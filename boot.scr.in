# mmc/scsi etc
setenv devtype mmc
# The device number of the given type
setenv devnum 0
# The partition containing the boot files (introduced in u-boot mainline 2016.01)
setenv distro_bootpart 1
# Address to load the config to
setenv config_addr_r 0xa00000
# Address to load the kernel to
setenv kernel_addr_r 0x5000000
# Address to load the FDT to
setenv fdt_addr_r 0x4f00000
# Address to load the initrd to.
setenv ramdisk_addr_r 0x8000000
# Kernel image filename
setenv kernel_name kernel.img
# Ramdisk filename
setenv ramdisk_name initrd.img 
# FDT filename
setenv fdt_name dtbs/armada-3720-espressobin.dtb

# load snap-generated configuration
load ${devtype} ${devnum}:${distro_bootpart} ${config_addr_r} uboot/ubuntu/boot.sel
env import ${config_addr_r}

#  prefix               Prefix within the boot partiion to the boot files
setenv prefix systems/${snapd_recovery_system}/kernel/

setenv console "console=ttyMV0,115200 earlycon=ar3700_uart,0xd0012000"
setenv bootargs "${console} rw snapd_recovery_mode=${snapd_recovery_mode} snapd_recovery_system=${snapd_recovery_system}"

# load all files
load ${devtype} ${devnum}:${distro_bootpart} ${kernel_addr_r} ${prefix}${kernel_name}
load ${devtype} ${devnum}:${distro_bootpart} ${fdt_addr_r} ${prefix}${fdt_name}
load ${devtype} ${devnum}:${distro_bootpart} ${ramdisk_addr_r} ${prefix}${ramdisk_name}
setenv ramdisk_size ${filesize}

# Boot the system
booti ${kernel_addr_r} ${ramdisk_addr_r}:${ramdisk_size} ${fdt_addr_r}
