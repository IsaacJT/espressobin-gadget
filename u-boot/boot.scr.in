# mmc/scsi etc
setenv devtype mmc
# The device number of the given type
setenv devnum 0
# The ubuntu-seed partition 
setenv seedpart 1
# The ubuntu-boot partition 
setenv bootpart 2
# Address to load the config to
setenv config_addr_r 0xa00000
# Address to load the kernel to
setenv kernel_addr_r 0x5000000
# Address to load the FDT to
setenv fdt_addr_r 0x4f00000
# Address to load the initrd to.
setenv ramdisk_addr_r 0x8000000
# Kernel image filename
setenv kernel_name kernel.img
# Ramdisk filename
setenv ramdisk_name initrd.img 
# FDT filename
setenv fdt_name dtbs/armada-3720-espressobin.dtb
# fitImage filename
setenv fitimage_name fitImage
# Use fitImage
setenv use_fitimage 0

echo "Loading ubuntu-seed configuration..."
load ${devtype} ${devnum}:${seedpart} ${config_addr_r} uboot/ubuntu/boot.sel
env import ${config_addr_r}

echo "Snapd boot mode: ${snapd_recovery_mode}"

if test "${snapd_recovery_mode}" = "run"
then
    echo "Loading ubuntu-boot configuration..."
    load ${devtype} ${devnum}:${bootpart} ${config_addr_r} uboot/ubuntu/boot.sel
    env import ${config_addr_r}
fi

# Prefix within the boot partiion to the boot files
setenv prefix systems/${snapd_recovery_system}/kernel/

setenv console "console=ttyMV0,115200 earlycon=ar3700_uart,0xd0012000"
setenv bootargs "${console} rw snapd_recovery_mode=${snapd_recovery_mode} snapd_recovery_system=${snapd_recovery_system}"

# Load all files
if test "${use_fitimage}" = "0"
then
    echo "Loading ${prefix}${kernel_name}..."
    load ${devtype} ${devnum}:${seedpart} ${kernel_addr_r} ${prefix}${kernel_name}
    echo "Loading ${prefix}${fdt_name}..."
    load ${devtype} ${devnum}:${seedpart} ${fdt_addr_r} ${prefix}${fdt_name}
    echo "Loading ${prefix}${ramdisk_name}..."
    load ${devtype} ${devnum}:${seedpart} ${ramdisk_addr_r} ${prefix}${ramdisk_name}
    setenv ramdisk_size ${filesize}

    booti ${kernel_addr_r} ${ramdisk_addr_r}:${ramdisk_size} ${fdt_addr_r}
else
    echo "Loading ${prefix}${fitimage_name}..."
    load ${devtype} ${devnum}:${seedpart} ${kernel_addr_r} ${prefix}${fitimage_name}
    bootm ${kernel_addr_r}
fi
